s                    : /[ \t]/ ;
n                    : /\r?\n/ ;
sign                 : '+' | '-' ;
string               : '"' /[^"]*/ '"' ; 
nonquoted_string     : /[^\r\n$]*/ ;
nonquoted_name_string     : /[^\r\n\s]*/ ;
integer              : <sign>? /[0-9]+/ ;
hexint               : /[0-9A-Fa-f]+/ ;
x                    : <float> <s>* ;
y                    : <float> <s>* ;
float                : /[-+]?[0-9]+(\.[0-9]+)?([eE][-+]?[0-9]+)?/ ;
header               : "EESchema-LIBRARY " /.*/ <n> ;
encoding             : '#' <s>* "encoding "* /.*/ <n> ;
symbol: <symboldef> <refdes_text> <name_text> <footprint_text> <datasheet_text> <user_text>* <aliases>* <fplist>? <drawing> "ENDDEF\n" <comment>*;
fplist: "$FPLIST\n" <fpalias>* "$ENDFPLIST\n";
fpalias: <nonquoted_string> <n>;
drawing:  "DRAW\n" (<arc>|<circle>|<polygon>|<rectangle>|<text>|<bezier>|<pin>)* "ENDDRAW\n" ;
symboldef: "DEF " <symbolname> <prefix> "0 " <pin_name_offset> <show_pin_numbers> <show_pin_names> <number_of_parts> <s>? <is_locked>? <s>? <is_power>? <n> ;
symbolname           : <nonquoted_name_string> <s> ;
prefix           : <nonquoted_name_string> <s> ;
pin_name_offset           : <float> <s> ;
show_pin_numbers           : ('Y' | 'N') <s> ;
show_pin_names           : ('Y' | 'N') <s> ;
number_of_parts           : <integer> ;
is_locked           : ('L' | 'F' | '0') ;
is_power           : ('P' | 'N');
size: <float> <s> ;
orientation: ('V' | 'H') <s> ;
visible: ('I' | 'V') <s> ;
halign: ('C'|'L'|'R') <s> ;
valign: ('T'|'C'|'B') ;
italic: ('I'|'N');
bold: ('B'|'N');
refdes_text       : "F0 " <string> <s> <x> <y> <size> <orientation> <visible> <halign> <valign> <italic>? <bold>? <n> ;
name_text         : "F1 " <string> <s> <x> <y> <size> <orientation> <visible> <halign> <valign> <italic>? <bold>? <n> ;
footprint_text    : "F2 " <string> <s> <x> <y> <size> <orientation> <visible> <halign> <valign> <italic>? <bold>? <n> ;
datasheet_text    : "F3 " <string> /.*/ <n>;
user_text         : "F" /[0-9]*/ " " <string> <s> <x> <y> <size> <orientation> <visible> <halign> <valign> <italic>? <bold>? <s>? <string>? <n>;
aliases: "ALIAS " <nonquoted_string>* <n> ;
arc               : "A " <x> <y> <radius> <start_angle> <end_angle> <part_index> <dmg> <pen> <fill> <s> <xstart> <ystart> <xend> <yend> <n> ;
radius: <float> <s>;
start_angle: <float> <s>;
end_angle: <float> <s>;
part_index: <integer> <s>;
dmg: <integer> <s>;
pen: <float> <s>;
fill: ('f'|'F'|'N');
xstart                    : <float> <s>* ;
ystart                    : <float> <s>* ;
xend                    : <float> <s>* ;
yend                    : <float> <s>* ;
circle: "C " <x> <y> <radius> <part_index> <dmg> <pen> <fill> <n> ;
polygon: "P " <count> <part_index> <dmg> <pen> <points> <fill> <n> ;
points: <point>* ;
point: <x> <y> ;
count: <integer> <s>;
rectangle: "S " <x1> <y1> <x2> <y2> <part_index> <dmg> <pen> <fill> <n> ;
x1                   : <float> <s> ;
y1                   : <float> <s> ;
x2                   : <float> <s> ;
y2                   : <float> <s> ;
italic_text: ("Italic"|"Normal") <s> ;
bold_text: ('1'|'0') <s> ;
text: "T " <angle> <x> <y> <size> <hidden> <part_index> <dmg> <name> <italic_text> <bold_text> <halign> <valign> <n>; 
hidden: ('0'|'1') <s>;
angle                    : <float> <s> ;
bezier: "B " <count> <part_index> <dmg> <pen> <points> <fill> <n>;
pin: "X " <nonquoted_name_string> <s> <pin_number> <x> <y> <length> <pin_orientation> <pin_num_size> <pin_name_size> <part_index> <dmg> <pin_type> <s>? <pin_shape>?  <n>;
pin_number: (<integer>|'~') <s>;
name:  (<string>|<nonquoted_name_string>) <s> ;
length: <float> <s>;
pin_orientation: ('U'|'D'|'L'|'R') <s>;
pin_num_size: <float> <s>;
pin_name_size: <float> <s>;
pin_type: ('I'|'O'|'B'|'T'|'P'|'C'|'E'|'N'|'W'|'w'|'1'|'U');
pin_shape: 'N'? ('I'|'C'|'V')? ;
comment: '#' /.*/ <n>;
anyline: /.*/ <n> ;
symbol_lib:    <header> <comment>* <encoding>* <comment>* <symbol>* ;
